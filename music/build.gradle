plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def setting = rootProject.ext.setting

def getApkName() {
    def packageName = rootProject.android.musicApplicationId
    // 用 "\\" 转义 "."
    def parts = packageName.split("\\.")
    // 获取最后一个部分
    def lastPart = parts[parts.length - 1]
    return lastPart
}


static def buildTime() {
    def date = new Date()
    def formattedDate = date.format("yyyy-MM-dd-hh-mm")
    return formattedDate
}

android {
    buildFeatures {
        viewBinding = true
    }

    compileSdkVersion rootProject.android.compileSdkVersion
    buildToolsVersion rootProject.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.android.musicApplicationId
        minSdkVersion rootProject.android.minSdkVersion
        targetSdkVersion rootProject.android.targetSdkVersion
        versionCode rootProject.android.versionCode
        versionName rootProject.android.versionName
        multiDexEnabled rootProject.android.multiDexEnabled

        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    //签名设置
    def alias = "commonui"
    def keyPW = "android"
    def filePath = "../app/src/main/jks/commonui.jks"
    def storePW = "wxdgut.com"
    signingConfigs {
        debug {
            keyAlias alias
            keyPassword keyPW
            storeFile file(filePath)
            storePassword(storePW)
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            keyAlias alias
            keyPassword keyPW
            storeFile file(filePath)
            storePassword(storePW)
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            setting.each { k, v ->
                buildConfigField("String", k, v)
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            setting.each { k, v ->
                buildConfigField("String", k, v)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //引用aar包
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    //自动追加版本号和版本名称
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            // 包名_版本号.apk
            outputFileName = "${getApkName()}_${buildType.name}_v${defaultConfig.versionName}_${buildTime()}.apk"
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', "*.aar"], dir: 'libs')
    implementation rootProject.depsLibs.appcompat
    implementation rootProject.depsLibs.material
    implementation rootProject.depsLibs.constraintlayout
    implementation rootProject.depsLibs.recyclerview
}